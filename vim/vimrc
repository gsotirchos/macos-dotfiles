"
" ~/.vim/vimrc
"

"""""""""""""""""""""""""""""""""""""""""""
set nocompatible  " be iMproved, required
filetype off  " required

" set the runtime path to include Vundle and initialize
set rtp+=$HOME/.vim/bundle/Vundle.vim
call vundle#begin()

" let Vundle manage Vundle, required
Plugin 'VundleVim/Vundle.vim'

"" plugins start here
Plugin 'dense-analysis/ale'
Plugin 'rizzatti/dash.vim'
Plugin 'xolox/vim-misc'
Plugin 'xolox/vim-easytags'
Plugin 'scrooloose/nerdcommenter'
Plugin 'tpope/vim-rsi'
Plugin 'sheerun/vim-polyglot'
Plugin 'bfrg/vim-cpp-modern'
Plugin 'vim-python/python-syntax'
Plugin 'lervag/vimtex'
"Plugin 'adimit/prolog.vim'
"Plugin 'PontusPersson/pddl.vim'
"Plugin 'JuliaEditorSupport/julia-vim'
"Plugin 'keith/swift.vim'
"Plugin 'neovimhaskell/haskell-vim'
"Plugin 'peterhoeg/vim-qml'
"Plugin 'PProvost/vim-ps1'
"Plugin 'yuezk/vim-js'

"" plugins end here
call vundle#end()  " required
"""""""""""""""""""""""""""""""""""""""""""

runtime $VIMRUNTIME/defaults.vim

filetype plugin indent on

" saving
set updatetime=400  " autoupdate delay
set undofile  " maintain undo file
set undodir=$HOME/.vim/undo/  " undo file location
set noswapfile  " don't write swap file
set nowritebackup  " no backup before overwriting
set nobackup  " no persistent backups
set autoread  " auto-read modified open files

" set encodings and line ending formats
set fileencodings=ucs-bom,utf-8,cp1253,default,latin1  " tried on open
set encoding=utf-8  " encoding used for displaying inside vim
set fileformats=unix,mac,dos  " format order to be tried on a new buffer

" appearance
set t_Co=16
colorscheme sunyata
set number  " show line numbers
set numberwidth=1  " smallest width at start
set wrap  " wrap long lines
set showmatch  " show matching parentheses
set noshowcmd  " hide typed command
let &fillchars = 'vert:│,diff:-,eob: ,lastline:.,fold: '
set showbreak=…  " show '…' at start of wrapped lines
set list  " show non-text characters
let &listchars = 'tab:|->,trail:·,conceal:◌,extends:…,precedes:…,nbsp:_'
function! MyFoldText()  " custom fold text
    let sub1 = substitute(getline(v:foldstart), '\(^\s*\S.\{,80}\S\)\%(\s.\{-}\)\=$', '\1', 'g')
    let sub2 = substitute(getline(v:foldend), '^\%(.\{-}\s*\)\=\(\S.\{,25}\)$', '\1', 'g')
    return (sub1 . ' ... ' . sub2 .'  (' . (v:foldend - v:foldstart + 1) . ' lines)')
endfunction
set foldtext=MyFoldText()
set laststatus=2  " always show statusline
function! MyStatusline()
    if (&ft == 'qf')
        " LocationList or QuickFix List
        let left_hand_side = '%( %q%)'
    elseif (&ft =~# '^.*preview.*$')
        " Preview window
        let left_hand_side = '%( %y%)'
    else
        " Regular editor
        let left_hand_side = '%( %y%)'

        if (&ft == '')
            let parent_dir = getcwd() . '/'
            let filetype_field_length = 0
            let half_name_length = 5  " len('[No Name]') / 2
        else
            if (&ft == 'help')
                let parent_dir = ''
            else
                let parent_dir = expand("%:~:h") . '/'
            endif

            let filetype_field_length = len(" [" . &ft . "]")  " <- len(' %y')
            let half_name_length = max([1, float2nr(floor(len(expand("%:~:t")) / 2.0))])
        endif

        let path_field_length = max([0, min([50,
        \   winwidth(0) / 2 - filetype_field_length - half_name_length
        \])])  " there is a 50 char hard limit
        let parent_dir_field = '%<%-' . path_field_length . '(' . parent_dir . '%)'
        let file_name_field = '%t%m%a   '
        let left_hand_side .= ' ' . parent_dir_field  . file_name_field
    endif
    let lines_field = '%' . (b:numberwidth + 2) . '(L: %l%)/%-' . (b:numberwidth + 1) . '(%L%)'
    let columns_field = '%6(C: %c%)/%-4(' . len(getline(".")) . '%)'
    return left_hand_side . '%=' . lines_field . columns_field
endfunction

" behavior
set timeoutlen=1500  " set leader timeout to 1.5s
"set regexpengine=1  " use old regex engine (WARNING: it's laggy)
set mouse=a  " enable mouse
set splitbelow  " open new horizontal windows at bottom
set splitright  " open new vertical windows at right
set previewheight=3  " set preview window height to 3
set autoindent  " autointent
set expandtab  " replace tabs with spaces
set sw=2   " ┐
set ts=4   " ├ 4 spaces per Tab, fold every 2
set sts=4  " ┘
set nojoinspaces  " don't insert 2 spaces after '.', '?' or '!'
set backspace=2  " allow backspace in insert mode
set scrolloff=3  " lines to scoll before the reaching last
set sidescrolloff=5  " columnts to sroll before reaching the last
set whichwrap+=h,l,<,>,[,]  " fix line movement on line borders
"set wrap lbr  " wrap lines by word
set formatoptions=1jnpr  " no automatic line breaking
set breakindent  " wrap preserving indentation
set display=lastline  " show last wrapped line in window
set shell=$SHELL\ -l  " login shell for 'term'
set shellcmdflag=-l\ -c  " login shell for '!'

" folding
set foldenable  " enable folding
set foldmethod=indent  " fold only based on intentation
set foldlevel=2  " depth of first folding
set foldnestmax=3  " depth of last folding
set foldcolumn=0  " hide fold column

" searching
set smartcase  " case sensitive only if Uppercase
set hlsearch  " highlight search matches
set incsearch  " do incremental search

" autocompletion
set completeopt=menuone,noinsert,preview  " select 1st item without inserting
set complete=t,.,i  " scan tags, curr. buffer, incl. files
set shortmess+=c  " shut off completion messages
set belloff+=ctrlg  " silent completion
set wildmenu  " display completion matches in a status line
let g:apc_enable_ft = {'*':1, 'cpp':0}  " auto popup
let g:apc_cr_confirm = 1  " return inserts selected

" mappings
runtime vimrc.d/mappings.vim

" autocommands
runtime vimrc.d/autocommands.vim
